{-# LANGUAGE OverloadedStrings #-}

module Lib
  ( run,
    pruneUnusedClasses,
    normaliseConfig,
    mkDefaultConfig,
    Config (..),
  )
where

import Control.Monad (join)
import qualified Data.Bifunctor as BiF
import qualified Data.Either as Either
import qualified Data.Maybe as Maybe
import Data.Text (Text)
import qualified Data.Text as Text
import qualified Data.Text.IO as TextIO
import Data.Tree (Tree (..))
import qualified Data.Tree as Tree
import Options.Applicative ((<**>))
import qualified Options.Applicative as Opt
import qualified System.Directory as Dir
import System.FilePath ((</>))
import qualified System.FilePath as Path
import Text.Parsec ((<|>))
import qualified Text.Parsec as P
import Text.Parsec.Text (Parser, parseFromFile)

newtype FileName = FileName FilePath
  deriving (Show, Eq, Ord)

newtype FullPath = FullPath FilePath
  deriving (Show, Eq, Ord)

newtype Extension = Extension String
  deriving (Show, Eq, Ord)

data Directory
  = Directory FileName FullPath [File]
  deriving (Show, Eq, Ord)

data File
  = File FileName FullPath Extension
  deriving (Show, Eq, Ord)

traverseDirectory :: FilePath -> FilePath -> IO (Tree Directory)
traverseDirectory path name = do
  contents <- filter (not . isIgnoredPath) <$> Dir.getDirectoryContents path
  (files, dirs) <-
    normaliseItems . Maybe.catMaybes <$> traverse (toItem path) contents
  pure $ Node (Directory (FileName name) (FullPath path) files) dirs

normaliseItems :: [Either File (Tree Directory)] -> ([File], [Tree Directory])
normaliseItems = BiF.bimap (filter isPursFile) (filter $ not . isEmpty) . Either.partitionEithers

toItem :: FilePath -> FilePath -> IO (Maybe (Either File (Tree Directory)))
toItem rootPath name = do
  let path = Path.normalise $ rootPath </> name
  exists <- Dir.doesPathExist path
  isSym <- Dir.pathIsSymbolicLink path
  isDir <- Dir.doesDirectoryExist path
  case (not exists || isSym, isDir) of
    (True, _) -> pure Nothing
    (_, True) -> Just . Right <$> traverseDirectory path name
    (_, False) -> pure $ Just $ Left $ File (FileName name) (FullPath path) (takeExtension name)

isPursFile :: File -> Bool
isPursFile (File _ _ (Extension "purs")) = True
isPursFile _ = False

isEmpty :: Tree Directory -> Bool
isEmpty (Node (Directory _ _ []) []) = True
isEmpty _ = False

-- TODO: use .gitignore
isIgnoredPath :: FilePath -> Bool
isIgnoredPath "." = True
isIgnoredPath ".." = True
isIgnoredPath ".git" = True
isIgnoredPath "node_modules" = True
isIgnoredPath ".stack-work" = True
isIgnoredPath _ = False

takeExtension :: FilePath -> Extension
takeExtension path =
  case Path.takeExtension path of
    ('.' : extension) -> Extension extension
    extension -> Extension extension

filePath :: File -> FilePath
filePath (File _ (FullPath path) _) = path

dirFiles :: Directory -> [File]
dirFiles (Directory _ _ files) = files

getRoot :: FilePath -> IO (FilePath, String)
getRoot path =
  (,)
    <$> Dir.makeRelativeToCurrentDirectory path
    <*> fmap Path.takeFileName Dir.getCurrentDirectory

listFiles :: FilePath -> IO [FilePath]
listFiles path = do
  Dir.setCurrentDirectory path
  (rootPath, rootName) <- getRoot path
  dir <- traverseDirectory rootPath rootName
  pure $ fmap filePath $ join $ Tree.flatten $ fmap dirFiles dir

-- -----------------------------------------------------------------------------

data CssClass
  = CssClass String String
  deriving (Eq, Ord)

instance Show CssClass where
  show (CssClass name css) =
    unlines
      [ "-- | " <> name,
        name <> " :: ClassName",
        name <> " = ClassName \"" <> css <> "\""
      ]

cssClass :: Parser CssClass
cssClass =
  CssClass
    <$> P.manyTill P.alphaNum (P.char ';')
    <*> P.many1 (P.alphaNum <|> P.oneOf "-:/")

classes :: Parser [CssClass]
classes = cssClass `P.endBy` P.spaces

-- -----------------------------------------------------------------------------

data AST
  = TailwindClass String
  | CharNode
  deriving (Eq, Ord, Show)

node :: Parser AST
node = P.try classP <|> charNodeP

classP :: Parser AST
classP = TailwindClass <$> (P.string "T." *> P.many P.alphaNum)

charNodeP :: Parser AST
charNodeP = CharNode <$ P.anyChar

getClassName :: AST -> Maybe String
getClassName (TailwindClass name) = Just name
getClassName _ = Nothing

classesP :: Parser [String]
classesP = Maybe.mapMaybe getClassName <$> P.many node

extractClasses :: FilePath -> IO (Either String [String])
extractClasses path = do
  contents <- TextIO.readFile path
  if "import Tailwind as T" `Text.isInfixOf` contents
    then pure $ BiF.first show $ P.parse classesP path contents
    else pure $ Right []

-- -----------------------------------------------------------------------------

tailwindPurs :: [CssClass] -> Text
tailwindPurs usedClasses =
  Text.unlines
    [ "-- | Autogenerated from tailwind.css",
      "module Tailwind where",
      "",
      "import Halogen.HTML.Core (ClassName(..))",
      "",
      classes
    ]
  where
    classes = Text.intercalate "\n" $ map (Text.pack . show) usedClasses

filterUsedClasses :: [String] -> [CssClass] -> [CssClass]
filterUsedClasses usedClasses =
  filter $ \(CssClass name _) -> name `elem` usedClasses

-- -----------------------------------------------------------------------------

data Config = Config
  { optRoot :: FilePath,
    optClasses :: FilePath,
    optSrc :: FilePath,
    optOut :: FilePath
  }
  deriving (Eq, Show)

configP :: Opt.Parser Config
configP =
  Config
    <$> Opt.strArgument
      ( Opt.metavar "ROOT"
          <> Opt.value "."
          <> Opt.help "Root directory of the project"
          <> Opt.showDefault
      )
    <*> Opt.strOption
      ( Opt.long "classes"
          <> Opt.metavar "CLASSES"
          <> Opt.value "css.txt"
          <> Opt.help "The file with the available classes"
          <> Opt.showDefault
      )
    <*> Opt.strOption
      ( Opt.long "src"
          <> Opt.metavar "SRC"
          <> Opt.value "src"
          <> Opt.help "The source directory"
          <> Opt.showDefault
      )
    <*> Opt.strOption
      ( Opt.long "out"
          <> Opt.short 'o'
          <> Opt.metavar "OUT"
          <> Opt.value "src/Tailwind.purs"
          <> Opt.help "The output file"
          <> Opt.showDefault
      )

opts :: Opt.ParserInfo Config
opts =
  Opt.info
    (configP <**> Opt.helper)
    ( Opt.fullDesc
        <> Opt.header "Do some Tailwind stuff"
    )

mkDefaultConfig :: FilePath -> Config
mkDefaultConfig root =
  Config
    { optRoot = root,
      optClasses = root </> "css.txt",
      optSrc = root </> "src",
      optOut = root </> "src/Tailwind.purs"
    }

normaliseConfig :: Config -> IO Config
normaliseConfig (Config root classes src out) = do
  root <- Dir.makeAbsolute root
  pure $
    Config
      { optRoot = root,
        optClasses = root </> classes,
        optSrc = root </> src,
        optOut = root </> out
      }

pruneUnusedClasses :: Config -> IO ()
pruneUnusedClasses config = do
  files <- listFiles $ optSrc config
  usedClasses <- fmap join . sequence <$> traverse extractClasses files
  availableClasses <- BiF.first show <$> parseFromFile classes (optClasses config)
  let cs = filterUsedClasses <$> usedClasses <*> availableClasses
  let result = (,) <$> (length <$> cs) <*> (tailwindPurs <$> cs)
  case result of
    Right (count, contents) -> do
      TextIO.writeFile (optOut config) contents
      putStrLn $ "Found " <> show count <> " used classes"
      putStrLn $ optOut config <> " updated succesfully!"
    Left err -> putStrLn err

run :: IO ()
run = pruneUnusedClasses =<< normaliseConfig =<< Opt.execParser opts
